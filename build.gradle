plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
}

ext {
    kotlinVersion = '1.3.72'
    junitVersion = '5.7.0-M1'
    junitRunnerVersion = '1.7.0-M1'
}

group 'dev.zerite.craftlib'
version '0.1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group rootProject.group
    version rootProject.version

    sourceCompatibility = targetCompatibility = '1.8'
    compileJava.options.encoding 'UTF-8'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
//        testImplementation 'junit:junit:4.12'
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.junit.platform:junit-platform-runner:$junitRunnerVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    jar {
        manifest.attributes(
                'Implementation-Title': 'Craftlib',
                'Implementation-Version': project.version
        )
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }
    }
}
